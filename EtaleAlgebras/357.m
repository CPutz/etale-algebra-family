Z := Integers(); 

intrinsic Etale357(p::RngIntElt) -> []
{Gives the list of possible etale algebras for
signature (3,5,7) and p}
	Qp := pAdicField(p,500);
	o := O(Qp!p);
	B<r,a> := CreateParameterSpace(PolynomialRing(Qp,2), <0,0>, car<[Z!b+o : b in Integers(p) | b notin [0,1]]>);
	R<t> := PolynomialRing(B);
	phi := 15*t^7 - 35*t^6 + 21*t^5;

	printf "computing etale algebras generated by phi - a\n";
	IndentPush();
	b0, L0, L0n := IsomorphismClassesFamEtale(phi - a);
	IndentPop();

	B<r,a> := CreateParameterSpace(PolynomialRing(Qp,2), <1,Infinity()>, car<[Z!b+o : b in Integers(p) | b ne 0]>);
	R<t> := PolynomialRing(B);
	phi := 15*t^7 - 35*t^6 + 21*t^5;

	printf "\ncomputing etale algebras generated by phi - a*p^5r\n";
	IndentPush();
	b1, L1, L1n := IsomorphismClassesFamEtale(phi - a*r^5);
	IndentPop();

	RZ<tZ> := PolynomialRing(PolynomialRing(Integers(),2));
	printf "\ncomputing etale algebras generated by phi - (1+a*p^3r)\n";
	IndentPush();
	b2, L2, L2n := IsomorphismClassesFamEtale(R!Evaluate(RZ!(phi - (1+a*r^3)), tZ+1));
	IndentPop();

	printf "\ncomputing etale algebras generated by p^7r*phi - a\n";
	IndentPush();
	b3, L3, L3n := IsomorphismClassesFamEtale(r^7*phi - a);
	IndentPop();

	if b0 and b1 and b2 and b3 then
		printf "\nConclusive about etale algebras for p=%o\n", p;
	else
		printf "\nInconclusive about etale algebras for p=%o\n", p;
	end if;

	return [<L0,L0n>, <L1,L1n>, <L2,L2n>, <L3,L3n>];
end intrinsic

intrinsic Etale257(p::RngIntElt) -> []
{Gives the list of possible etale algebras for
signature (2,5,7) and p}
	if p eq 2 then
		D := LocalFieldDatabaseOctic2Adics();
	else
		D := LocalFieldDatabase();
	end if;

	Qp := pAdicField(p,500);
	o := O(Qp!p);
	B<r,a> := CreateParameterSpace(PolynomialRing(Qp,2), <0,0>, car<[Z!b+o : b in Integers(p) | b notin [0,1]]>);
	R<t> := PolynomialRing(B);
	phi1 := 4*t^5*(14 + 14*t + 20*t^2 + 25*t^3);
	phi2 := 4*t - 1;

	printf "computing etale algebras generated by phi1 - a*phi2\n";
	IndentPush();
	b0, L0, L0n := IsomorphismClassesFamEtale(phi1 - a*phi2: D := D);
	IndentPop();

	B<r,a> := CreateParameterSpace(PolynomialRing(Qp,2), <1,Infinity()>, car<[Z!b+o : b in Integers(p) | b ne 0]>);
	R<t> := PolynomialRing(B);
	phi1 := 4*t^5*(14 + 14*t + 20*t^2 + 25*t^3);
	phi2 := 4*t - 1;

	printf "\ncomputing etale algebras generated by phi1 - a*p^5r*phi2\n";
	IndentPush();
	b1, L1, L1n := IsomorphismClassesFamEtale(phi1 - a*r^5*phi2: D := D);
	IndentPop();

	RZ<tZ> := PolynomialRing(PolynomialRing(Integers(),2));
	printf "\ncomputing etale algebras generated by phi1 - (1+a*p^2r)*phi2\n";
	IndentPush();
	b2, L2, L2n := IsomorphismClassesFamEtale(R!Evaluate(RZ!(phi1 - (1+a*r^2)*phi2), tZ+1): D := D);
	IndentPop();

	printf "\ncomputing etale algebras generated by p^7r*phi1 - a*phi2\n";
	IndentPush();
	b3, L3, L3n := IsomorphismClassesFamEtale(r^7*phi1 - a*phi2: D := D);
	IndentPop();

	if b0 and b1 and b2 and b3 then
		printf "\nConclusive about etale algebras for p=%o\n", p;
	else
		printf "\nInconclusive about etale algebras for p=%o\n", p;
	end if;

	return [<L0,L0n>, <L1,L1n>, <L2,L2n>, <L3,L3n>];
end intrinsic

intrinsic Etale257_4z() -> <>
{Gives the list of possible etale algebras for
signature (2,5,7) and p}
	p := 2;
	Qp := pAdicField(p,500);
	o := O(Qp!p);
	D := LocalFieldDatabaseOctic2Adics();
	B<r,a> := CreateParameterSpace(PolynomialRing(Qp,2), <2,Infinity()>, car<[Z!b+o : b in Integers(p) | b ne 0]>);
	R<t> := PolynomialRing(B);
	phi1 := 4*t^5*(14 + 14*t + 20*t^2 + 25*t^3);
	phi2 := 4*t - 1;

	printf "\ncomputing etale algebras generated by p^7r*phi1 - a*phi2 when 4|z\n";
	IndentPush();
	b3, L3, L3n := IsomorphismClassesFamEtale(r^7*phi1 - a*phi2: D := D);
	IndentPop();

	if b3 then
		printf "\nConclusive about etale algebras\n";
	else
		printf "\nInconclusive about etale algebras\n";
	end if;

	return <L3,L3n>;
end intrinsic
