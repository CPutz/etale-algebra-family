Z := Integers(); 

intrinsic Etale357(p::RngIntElt) -> []
{Gives the list of possible etale algebras for
signature (3,5,7) and p}
	Qp := pAdicField(p,500);
	o := O(Qp!p);
	B<r,a> := CreateParameterSpace(PolynomialRing(Qp,2), <0,0>, car<[Z!b+o : b in Integers(p) | b notin [0,1]]>);
	R<t> := PolynomialRing(B);
	phi := 15*t^7 - 35*t^6 + 21*t^5;

	if p eq 2 then
		b := true; L0 := {}; L0n := {};
	else
		printf "computing etale algebras generated by phi - a\n";
		IndentPush();
		b0, L0, L0n := IsomorphismClassesFamEtale(phi - a);
		IndentPop();
	end if;

	B<r,a> := CreateParameterSpace(PolynomialRing(Qp,2), <1,Infinity()>, car<[Z!b+o : b in Integers(p) | b ne 0]>);
	R<t> := PolynomialRing(B);
	phi := 15*t^7 - 35*t^6 + 21*t^5;

	printf "\ncomputing etale algebras generated by phi - a*p^5r\n";
	IndentPush();
	b1, L1, L1n := IsomorphismClassesFamEtale(phi - a*r^5);
	IndentPop();

	RZ<tZ> := PolynomialRing(PolynomialRing(Integers(),2));
	printf "\ncomputing etale algebras generated by phi - (1+a*p^3r)\n";
	IndentPush();
	b2, L2, L2n := IsomorphismClassesFamEtale(R!Evaluate(RZ!(phi - (1+a*r^3)), tZ+1));
	IndentPop();

	printf "\ncomputing etale algebras generated by p^7r*phi - a\n";
	IndentPush();
	b3, L3, L3n := IsomorphismClassesFamEtale(r^7*phi - a);
	IndentPop();

	if b0 and b1 and b2 and b3 then
		printf "\nConclusive about etale algebras for p=%o\n", p;
	else
		printf "\nInconclusive about etale algebras for p=%o\n", p;
	end if;

	return [<L0,L0n>, <L1,L1n>, <L2,L2n>, <L3,L3n>];
end intrinsic