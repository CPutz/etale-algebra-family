// Local covering sets for the rank 11 local algebras at 2, 3, 5 and 11 generated by
// the solutions to the standard GFE with signature (3,5,11).
// This data is verified in local_algebras.m

// Load this file from the main folder
AttachSpec("spec");

// ------------- At the prime 2 -------------
Q2 := pAdicField(2,500);
U2 := [EtaleAlgebra([UnramifiedExtension(Q2,n) : n in e], Q2) : e in [[10,1], [5,3,2,1], [4,4,2,1]]];


// ------------- At the prime 3 -------------
Q3 := pAdicField(3,500);
_<t> := PolynomialRing(Q3);
Q32 := UnramifiedExtension(Q3,2);
_<t2> := PolynomialRing(Q32);
i := Roots(t^2 + 1, Q32)[1,1];

// The case v3(a) = 4
U3_va4 := [
	EtaleAlgebra(DefiningPolynomial(ext<Q32 | t2^3 + 3*i*t2 + 3>,Q3)
		* (t^4 + 2*t^3 + 2) * t),
	EtaleAlgebra((t^5 - 3) * (t^4 + 2*t^3 + 2) * t * (t+1)),
	EtaleAlgebra((t^3 + 3*t + 3) * (t^3 - 3*t + 3) * (t^4 + 2*t^3 + 2) * t)];

E3_t5_3 := ext<Q3 | t^5 - 3>;

// The case v3(a) = 6
U3_va6 := [EtaleAlgebra(
	[E3_t5_3] cat [UnramifiedExtension(Q3,e) : e in es], Q3)
		: es in [[4,1,1],[3,2,1],[6]]];

// The cases case v3(a) = 7,8,10
U3_va_7_8_10 := [EtaleAlgebra([E3_t5_3, E3_t5_3, Q3],Q3)];

// The case case v3(a) = 9
U3_va9 := [EtaleAlgebra((t^5 - 3) * (t^4 + 2*t^3 + 2) * t * (t+1))];

// The case v3(b) = 3
U3_vb3 := [
	EtaleAlgebra((t^5 - 3) * (t^2 - 3) * (t^2 + 3) * t * (t+1)),
	EtaleAlgebra((t^5 - 3) * (t^3 + 3*t + 3) * (t^2 - 3) * t)];

// The case v3(c) = 6
U3_vc6 := [EtaleAlgebra([UnramifiedExtension(Q3,e) : e in es], Q3)
		: es in [[5,5,1], [5,4,2], [7,3,1]]];


// ------------- At the prime 5 -------------
Q5 := pAdicField(5,500);
_<t> := PolynomialRing(Q5);

// The case v5(b) = 4
U5_vb4 := [EtaleAlgebra((t^5 + 10*t + 5) * (t^2 - 5) * (t^2 + 4*t + 2) * t * (t+1))];

// The case v5(c) = 10
U5_vc10 := [EtaleAlgebra([UnramifiedExtension(Q5,e) : e in es], Q5)
	: es in [[11], [7,3,1], [5,5,1], [5,4,2], [4,3,2,1,1]]];


// ------------- At the prime 11 -------------
Q11 := pAdicField(11,500);
_<t> := PolynomialRing(Q11);
Q11_5 := UnramifiedExtension(Q11,5);
_<t5> := PolynomialRing(Q11_5);

// The case v11(a) = 8
U11_va8 := [
	DirectProduct([EtaleAlgebra(t^2 + 11) : i in [1..5]] cat [EtaleAlgebra(t)]),
	EtaleAlgebra(DefiningPolynomial(ext<Q11_5 | t5^2 + 11>, Q11) * t)];

// The case v11(b) = 4
U11_vb4 := [EtaleAlgebra([ext<Q11 | t^8 + 11>, UnramifiedExtension(Q11,2), Q11], Q11)];